// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

//  См. ДСП_КлиентСервер.НастройкиРасширения
Функция НастройкиРасширения(Знач НужноОбновить = Неопределено) Экспорт
	
	КэшированныеНастройки = Неопределено; // см. ДСП_КлиентСервер.НовыйКэшированныеНастройкиРасширения
	Если НужноОбновить = Неопределено Тогда
		КэшированныеНастройки = ДСП_ПовтИсп.КэшированныеНастройкиРасширения();
		НужноОбновить = ДСП_КлиентСервер.НастройкиНужноОбновить(КэшированныеНастройки);
	КонецЕсли;
	
	Если НужноОбновить Тогда
		
		НастройкиРасширения = НастройкиРасширенияИзБазы();
		Если КэшированныеНастройки <> Неопределено Тогда
			НовыеКэшНастроек = ДСП_КлиентСервер.НовыйКэшированныеНастройкиРасширения(НастройкиРасширения);
			ЗаполнитьЗначенияСвойств(КэшированныеНастройки, НовыеКэшНастроек);
		КонецЕсли;
		
	Иначе
		
		Если КэшированныеНастройки = Неопределено Тогда
			КэшированныеНастройки = ДСП_ПовтИсп.КэшированныеНастройкиРасширения();
		КонецЕсли;
		
		НастройкиРасширения = КэшированныеНастройки.Настройки;
		
	КонецЕсли;
	
	Возврат НастройкиРасширения;
	
КонецФункции

// Записать настройки расширения.
// 
// Параметры:
//  НастройкиВБазу - см. ДСП_КлиентСервер.НовыйНастройкиРасширения
Процедура ЗаписатьНастройкиРасширения(НастройкиВБазу) Экспорт
	
	ШаблонНастроек = ДСП_КлиентСервер.НовыйНастройкиРасширения();
	ОбработатьНастройки(ШаблонНастроек, НастройкиВБазу);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыОтбора = ПараметрыОтбораХранилища();
	ХранилищеНастроек().Сохранить(
		ПараметрыОтбора.КлючОбъекта,
		ПараметрыОтбора.КлючНастроек,
		ШаблонНастроек,,
		ПараметрыОтбора.ИмяПользователя);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Настройки

// Настройки расширения из базы.
// 
// Возвращаемое значение:
//  см. ДСП_КлиентСервер.НовыйНастройкиРасширения
Функция НастройкиРасширенияИзБазы()
	
	НастройкиРасширения = ДСП_КлиентСервер.НовыйНастройкиРасширения();

	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыОтбора = ПараметрыОтбораХранилища();
	НастройкиИзБазы = ХранилищеНастроек().Загрузить(
		ПараметрыОтбора.КлючОбъекта, 
		ПараметрыОтбора.КлючНастроек,,
		ПараметрыОтбора.ИмяПользователя);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОбработатьНастройки(НастройкиРасширения, НастройкиИзБазы);
	
	Возврат НастройкиРасширения;
	
КонецФункции

Функция ХранилищеНастроек()
	
	Возврат ХранилищеСистемныхНастроек;
	
КонецФункции

Функция ПараметрыОтбораХранилища()
	
	Результат = Новый Структура;
	Результат.Вставить("КлючОбъекта", ДСП_КлиентСервер.ИмяРасширения());
	Результат.Вставить("КлючНастроек", "Настройки");
	Результат.Вставить("ИмяПользователя", "_ОбщийПользователь_");
	
	Возврат Результат;
	
КонецФункции

// Обработать настройки.
// 
// Параметры:
//  ПриемникНастроек - см. ДСП_КлиентСервер.НастройкиРасширения
//  ИсточникНастроек - Неопределено, Структура - Настройки из базы
Процедура ОбработатьНастройки(ПриемникНастроек, ИсточникНастроек)
	
	Если ТипЗнч(ИсточникНастроек) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ДСП_КлиентСервер.ЗаполнитьСтруктуруРекурсивно(ПриемникНастроек, ИсточникНастроек);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти