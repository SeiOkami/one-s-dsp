// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Настройки расширения
// 
// Возвращаемое значение:
//  См. ДСП_КлиентСервер.НастройкиРасширения
Функция НастройкиРасширения() Экспорт
	
	НастройкиРасширения = ДСП_КлиентСервер.НовыйНастройкиРасширения();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыОтбора = ПараметрыОтбораХранилища();
	НастройкиИзБазы = ХранилищеНастроек().Загрузить(
		ПараметрыОтбора.КлючОбъекта, 
		ПараметрыОтбора.КлючНастроек,,
		ПараметрыОтбора.ИмяПользователя);
	
	УстановитьПривилегированныйРежим(Ложь);
		
	Если ТипЗнч(НастройкиИзБазы) = Тип("Структура") Тогда
		ДСП_КлиентСервер.ЗаполнитьСтруктуруРекурсивно(НастройкиРасширения, НастройкиИзБазы);
	КонецЕсли;
	
	Возврат НастройкиРасширения;
	
КонецФункции

// Записать настройки расширения.
// 
// Параметры:
//  Настройки - см. ДСП_КлиентСервер.НовыйНастройкиРасширения
Процедура ЗаписатьНастройкиРасширения(Настройки) Экспорт
	
	ПустыеНастройки = ДСП_КлиентСервер.НовыйНастройкиРасширения();
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		ДСП_КлиентСервер.ЗаполнитьСтруктуруРекурсивно(ПустыеНастройки, Настройки);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыОтбора = ПараметрыОтбораХранилища();
	ХранилищеНастроек().Сохранить(
		ПараметрыОтбора.КлючОбъекта, 
		ПараметрыОтбора.КлючНастроек,
		Настройки,,
		ПараметрыОтбора.ИмяПользователя);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ХранилищеНастроек()
	
	Возврат ХранилищеСистемныхНастроек;
	
КонецФункции

Функция ПараметрыОтбораХранилища()
	
	Результат = Новый Структура;
	Результат.Вставить("КлючОбъекта", ДСП_КлиентСервер.ИмяРасширения());
	Результат.Вставить("КлючНастроек", "Настройки");
	Результат.Вставить("ИмяПользователя", "_ОбщийПользователь_");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти