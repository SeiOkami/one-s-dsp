// @strict-types

#Область ПрограммныйИнтерфейс

// Настройки расширения.
// 
// Параметры:
//  ИзКэша - Булево - Из кэша или напрямую из базы
// 
// Возвращаемое значение: 
//  см. ДСП_ТипыКлиентСервер.НастройкиРасширения
Функция НастройкиРасширения(ИзКэша = Истина) Экспорт
	
	Если ИзКэша Тогда
		Возврат ДСП_НастройкиПовтИсп.НастройкиРасширения();
	КонецЕсли;
	
	НастройкиРасширения = ДСП_ТипыКлиентСервер.НастройкиРасширения();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыОтбора = ПараметрыОтбораХранилища();
	НастройкиИзБазы = ХранилищеНастроек().Загрузить(
		ПараметрыОтбора.КлючОбъекта, 
		ПараметрыОтбора.КлючНастроек,,
		ПараметрыОтбора.ИмяПользователя);
	
	УстановитьПривилегированныйРежим(Ложь);
		
	Если ТипЗнч(НастройкиИзБазы) = Тип("Структура") Тогда
		ДСП_ОбщееКлиентСервер.ЗаполнитьСтруктуруРекурсивно(НастройкиРасширения, НастройкиИзБазы);
	КонецЕсли;
	
	Возврат НастройкиРасширения;
	
КонецФункции

// Записать настройки расширения.
// 
// Параметры:
//  Настройки - см. ДСП_ТипыКлиентСервер.НастройкиРасширения
Процедура ЗаписатьНастройкиРасширения(Настройки) Экспорт
	
	ПустыеНастройки = ДСП_ТипыКлиентСервер.НастройкиРасширения();
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		ДСП_ОбщееКлиентСервер.ЗаполнитьСтруктуруРекурсивно(ПустыеНастройки, Настройки);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыОтбора = ПараметрыОтбораХранилища();
	ХранилищеНастроек().Сохранить(
		ПараметрыОтбора.КлючОбъекта, 
		ПараметрыОтбора.КлючНастроек,
		Настройки,,
		ПараметрыОтбора.ИмяПользователя);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ХранилищеНастроек()
	
	Возврат ХранилищеСистемныхНастроек;
	
КонецФункции

Функция ПараметрыОтбораХранилища()
	
	Результат = Новый Структура;
	Результат.Вставить("КлючОбъекта", ДСП_ТипыКлиентСервер.ИмяРасширения());
	Результат.Вставить("КлючНастроек", "Настройки");
	Результат.Вставить("ИмяПользователя", "_ОбщийПользователь_");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти