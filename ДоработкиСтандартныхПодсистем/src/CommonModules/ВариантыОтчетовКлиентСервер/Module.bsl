// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Внедрить настройки.
// 
// Параметры:
//  НастройкиОтчета - Структура
//  ВнедряемыеНастройки - см. ДСП_НовыйНастройкиОтчета
Процедура ДСП_ВнедритьНастройки(НастройкиОтчета, ВнедряемыеНастройки) Экспорт
	
	КлючНастроек = ДСП_КлючНастроек();
	НастройкиОтчета.Вставить(КлючНастроек, ВнедряемыеНастройки);
	
КонецПроцедуры

// Ключ настроек ДСП, внедряемых в основные настройки.
// 
// Возвращаемое значение:
//  Строка
Функция ДСП_КлючНастроек() Экспорт
	
	Возврат "ДСП_НастройкиОтчета";
	
КонецФункции

// Новый настройки отчета.
// 
// Возвращаемое значение:
//  Структура:
// * ФормаКлиентскихСобытий - Строка - Имя формы отчета, в которой содержатся клиентские события
// * События - Структура -:
// ** ПослеФормирования - Булево - см. ОтчетыКлиентПереопределяемый.ПослеФормирования
// ** ОбработкаРасшифровки - Булево - см. ОтчетыКлиентПереопределяемый.ОбработкаРасшифровки
// ** ОбработкаДополнительнойРасшифровки - Булево - см. ОтчетыКлиентПереопределяемый.ОбработкаДополнительнойРасшифровки
// ** ПриНачалеВыбораЗначений - Булево - см. ОтчетыКлиентПереопределяемый.ПриНачалеВыбораЗначений
// ** ОбработчикКоманды - Булево - см. ОтчетыКлиентПереопределяемый.ОбработчикКоманды
// ** ОбработкаВыбора - Булево - см. ОтчетыКлиентПереопределяемый.ОбработкаВыбора
// ** ОбработкаВыбораТабличногоДокумента - Булево - см. ОтчетыКлиентПереопределяемый.ОбработкаВыбораТабличногоДокумента
// ** ОбработкаОповещения - Булево - см. ОтчетыКлиентПереопределяемый.ОбработкаОповещения
// ** ПриНажатииКнопкиВыбораПериода - Булево - см. ОтчетыКлиентПереопределяемый.ПриНажатииКнопкиВыбораПериода
Функция ДСП_НовыйНастройкиОтчета() Экспорт
	
	События = Новый Структура;
	События.Вставить("ПослеФормирования", Ложь);
	События.Вставить("ОбработкаРасшифровки", Ложь);
	События.Вставить("ОбработкаДополнительнойРасшифровки", Ложь);
	События.Вставить("ОбработчикКоманды", Ложь);
	События.Вставить("ПриНачалеВыбораЗначений", Ложь);
	События.Вставить("ОбработкаВыбора", Ложь);
	События.Вставить("ОбработкаВыбораТабличногоДокумента", Ложь);
	События.Вставить("ОбработкаОповещения", Ложь);
	События.Вставить("ПриНажатииКнопкиВыбораПериода", Ложь);
	
	Результат = Новый Структура;
	Результат.Вставить("ФормаКлиентскихСобытий", "");
	Результат.Вставить("События", События);
	
	Возврат Результат;
	
КонецФункции

// Настройки отчета.
// 
// Параметры:
//  ФормаОтчета - см. ОтчетыКлиентПереопределяемый.ДСП_ОписаниеОбщейФормыОтчета
// 
// Возвращаемое значение:
//  Неопределено - Настройки не внедрены
//  - см. ДСП_НовыйНастройкиОтчета
Функция ДСП_НастройкиОтчета(ФормаОтчета) Экспорт
	
	Результат = Неопределено;
	
	Если ДСП_КлиентСервер.Настройка_ВариантыОтчетов_РасширенныеСобытияФормы() Тогда
		
		КлючНастроек = ДСП_КлючНастроек();
	
		СвойстваФормы = Новый Структура("НастройкиОтчета", Неопределено);
		ЗаполнитьЗначенияСвойств(СвойстваФормы, ФормаОтчета);
		
		НастройкиОтчета = СвойстваФормы.НастройкиОтчета;
		
		НастройкиИТМ = Неопределено;
		Если ТипЗнч(НастройкиОтчета) = Тип("Структура")
			И НастройкиОтчета.Свойство(КлючНастроек, НастройкиИТМ) Тогда
			Результат = НастройкиИТМ;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
